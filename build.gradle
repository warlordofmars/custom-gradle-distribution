import org.gradle.distribution.DownloadGradle   


buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { 
            url "https://jitpack.io"
        }
        maven {
            url "https://plugins.gradle.org/m2/" 
        }
    }
    dependencies {
        classpath 'com.github.warlordofmars:gradle-release-helper:release-0.1.2'
    }
}

group = 'com.github.warlordofmars'

apply plugin: 'com.github.warlordofmars.gradle.release'
apply plugin: 'base'


ext {
    gradleVersion = '5.1.1'
    companyName = 'warlordofmars'
}

task('makeBuildDir') {
    doFirst {
        file(buildDir).mkdirs()
    }
}

task downloadGradle(type: DownloadGradle) {
    dependsOn makeBuildDir
    description = 'Downloads the Gradle distribution with a given version.'
    gradleVersion = project.gradleVersion
    destinationDir = file(buildDir)
}

task customGradleDistribution(type: Zip, dependsOn: downloadGradle) {
    description = 'Builds custom Gradle distribution and bundles initialization scripts.'
    archiveName "${companyName}-gradle-${downloadGradle.gradleVersion}-${version}-bin.zip"

    from zipTree(downloadGradle.destinationFile)
    into "$downloadGradle.distributionNameBase/init.d", {
        from 'src/init.d'
    }
}

defaultTasks 'customGradleDistribution'

build.dependsOn customGradleDistribution

githubRelease.releaseAssets customGradleDistribution.archivePath